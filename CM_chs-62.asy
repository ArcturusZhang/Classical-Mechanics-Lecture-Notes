if(!settings.multipleView) settings.batchView=false;
settings.tex="xelatex";
defaultfilename="CM_chs-62";
if(settings.render < 0) settings.render=4;
settings.outformat="";
settings.inlineimage=true;
settings.embed=true;
settings.toolbar=false;
viewportmargin=(2,2);

texpreamble("\usepackage{xeCJK}");
texpreamble("\setCJKmainfont{SimSun}");
usepackage("amsmath");
import graph;
import math;
size(300);
//强迫振动的相位差与频率比之间的关系
picture tmp;
pair O;
real gamma,xs,l;
real delta(real omega){
real de;
if(omega==1) return pi/2;
de = atan(gamma*omega/(1-omega**2));
if(de<0) de=de+pi;
return de;
}
xs = 2;
draw(Label("$\dfrac{\omega_p}{\omega_0}$",EndPoint),xscale(xs)*(O--(2.75,0)),Arrow);
draw(Label("$\delta$",EndPoint),O--(0,1.1*pi),Arrow);
gamma = 1;
draw(tmp,graph(delta,0,2.5),red+linewidth(1bp));
gamma = 0.5;
draw(tmp,graph(delta,0,2.5),orange+linewidth(1bp));
gamma = 0.25;
draw(tmp,graph(delta,0,2.5),green+linewidth(1bp));
draw(tmp,O--(1,0)--(1,pi)--(2.5,pi),blue+linewidth(1bp));
draw(tmp,(0,pi/2)--(1,pi/2),dashed);
draw(tmp,(0,pi)--(1,pi),dashed);
l = 0.05;
for(int i=1;i<=5;i=i+1){
draw(tmp,(0.5*i,l)--(0.5*i,0));
}
add(xscale(xs)*tmp);
label("$\dfrac{\pi}{2}$",(0,pi/2),W);
label("$\pi$",(0,pi),W);
label("$0$",xs*(0,0),S);
label("$0.5$",xs*(0.5*1,0),S);
label("$1.0$",xs*(0.5*2,0),S);
label("$1.5$",xs*(0.5*3,0),S);
label("$2.0$",xs*(0.5*4,0),S);
label("$2.5$",xs*(0.5*5,0),S);
real height,width,right,top;
pair P;
path legendbox;
right = 2.6;
top = pi/2+0.5;
width = 0.55;
height = 1.2;
legendbox = xscale(xs)*box((right,top),(right-width,top-height));
fill(shift(0.06,-0.06)*legendbox,black);
unfill(legendbox);
draw(legendbox);
P = (right-width/2,top-1*height/8);
label("$\gamma=0$",xscale(xs)*P,blue);
P = (right-width/2,top-3*height/8);
label("$\gamma=0.25$",xscale(xs)*P,green);
P = (right-width/2,top-5*height/8);
label("$\gamma=0.5$",xscale(xs)*P,orange);
P = (right-width/2,top-7*height/8);
label("$\gamma=1$",xscale(xs)*P,red);
